#!/usr/bin/perl -w
#
# extract tracking number from e-mail, and invoke "ips" to add it to tracking database
# Started by Matija Nalis <mnalis-perl@voyager.hr> 2010-12-26, GPLv3+
#

use strict;

use MIME::Base64;

my $DEBUG = 0;

my %found=();
my $full_decoded='';

umask 002;

sub found_tracking($) {
	my ($tracking) = @_;
	print "Found Tracking# $tracking\n" if $DEBUG > 1;
	$found{$tracking}++;
	return '';
}

# checks if given line contains tracking number
# example tracking numbers: EA123456789HR, RT072868631HK, RA004359364CN
sub check_tracking($) {	
	my ($line) = @_;
	$line =~ s/\b([A-Z][A-Z0-9][A-Z0-9]\d{8}[A-Z]{2})\b/found_tracking($1)/ge;
	return undef;
}

my $header = 1;
my $from_continue = 0;
my $sender = '';
while (<STDIN>) {
	print "line   : $_" if $DEBUG > 2;
	if ($header and !$sender) {
		$header = 0 if /^\s*$/;		# detect end of headers
		if ($from_continue) {
			$from_continue = 0;
			print "Continuation line is: $_" if $DEBUG > 1;
			if (/^\s.*?<(.+?)>/) {
				$sender = $1;
				print "Found sender(2): <$sender>\n" if $DEBUG > 1;
			}
		}
		if (/^From:\s/) { 
			if (/^From:\s.*<(.+?)>/) {
				$sender = $1;
				print "Found sender: <$sender>\n" if $DEBUG > 1;
			} else {
				$from_continue = 1;
				print "Found From: continuation\n" if $DEBUG > 1;
			}
		}
	}
	check_tracking ($_);
	if (m!^([A-Za-z0-9+/=]{1,76})\s*$!) {	# detected Base64
		$full_decoded .= decode_base64($1);
	}
}

check_tracking($full_decoded) if $full_decoded;	# if it was base64 MIME e-mail...

foreach my $tn (keys %found) {
	print "Found key: $tn -- $found{$tn} times (sender: $sender)\n" if $DEBUG > 0;
	system 'ips', $tn, '', $sender;
}

exit 0;
