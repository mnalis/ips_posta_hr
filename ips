#!/usr/bin/perl -wT
# Matija Nalis <mnalis-perl@voyager.hr> 2010-11-18, GPLv3+
# prints any changes to packages status on ips.posta.hr

use strict;

my $DEBUG=0;
my $SPOOL_DIR='/var/local/ips_posta_hr';
my $AGENT_ID='check_ips/0.1 ';

$ENV{'PATH'}='/usr/local/bin:/usr/bin:/bin';


#
# no user serviceable parts below
#

use HTML::TableExtract;
use LWP::UserAgent;
use Data::Dumper qw(Dumper);

my $doit = 0;

my $ua = LWP::UserAgent->new;
$ua->agent($AGENT_ID);

# parses HTML file on disk
sub parse_raw_html($)
{
         my ($TRACKID) = @_;
         
         my $rawfile = "${SPOOL_DIR}/$TRACKID.raw";
         my $parsedfile = "${SPOOL_DIR}/$TRACKID.txt";
         my $newfile = "${parsedfile}.new";
         my $te = HTML::TableExtract->new( headers => [ 'Local Date and Time', 'Country', 'Location', 'Event Type', 'Extra Information' ] );	# match table with this specific headers
         $te->parse_file($rawfile);
         my $table = $te->first_table_found;

         $DEBUG && print "Table  found at ", join(',', $table->coords), ":\n";
         open TXT, '>', $newfile or die "can't create $newfile: $!";
         my $parsed_ok=0;
         foreach my $row ($table->rows) {
                no warnings;
                my $line = join(',', @$row);
                use warnings;
                $DEBUG && print "    x $line\n";
                print TXT "$line\n" or die "can't write to $newfile: $!";
                $parsed_ok++ if $line =~ m,^\d+/\d+/\d+ \d+:\d+:\d+,;
         }
         close (TXT) or die "can't write-close $newfile: $!";
         print "WARNING: parser looks invalid for $newfile\n" unless $parsed_ok;

         my $diff = undef;
         if (-r $parsedfile) {
                $DEBUG && print "Found $parsedfile, diff to $newfile\n";
                $diff = `diff $parsedfile $newfile | egrep '^[<>]'`;
         } else {
                $DEBUG && print "No $parsedfile, diff /dev/null to $newfile\n";
                $diff = `diff /dev/null $newfile | egrep '^[<>]'`;
         }
         if ($diff) {
                print "\n*** Changes found for tracked package# $TRACKID\n";
                print "$diff";
                if ($doit) {
                      $DEBUG && print "DOIT=1, doing it for real\n";
                      rename $newfile, $parsedfile or die "can't rename $newfile to $parsedfile: $!";
                } else {
                      print "(not commiting changes, call with 'DOIT' parameter to really do it)\n";
                }
         }
                                                           
}

# gets raw HTML for package, and calls parse_raw_html() to parse it
sub request_package_status($)
{
         my ($TRACKID) = @_;
         
         $DEBUG && print "\nRequesting package with tracking# $TRACKID\n";
         my $rawfile = "${SPOOL_DIR}/$TRACKID.raw";
         my $tmpfile = "${rawfile}.tmp";
         open RAW, '>', $tmpfile or die "can't write to $tmpfile: $!";
         
         # Create a request
         my $req = HTTP::Request->new(GET => "http://ips.posta.hr/IPSWeb_item_events.asp?itemid=$TRACKID");

         # Pass request to the user agent and get a response back
         my $res = $ua->request($req);

         # Check the outcome of the response
         if ($res->is_success) {
             print "CONTENT: " . $res->content if $DEBUG > 2;
             print RAW $res->content;
             close RAW;
             rename $tmpfile, $rawfile or die "can't rename $tmpfile to $rawfile: $!";
             parse_raw_html($TRACKID);
         }
         else {
             print "HTTP REQUEST FOR TRACKING# $TRACKID failed with ", $res->status_line, "\n";
         }
}


############### MAIN loop ################
 
my $arg = $ARGV[0];
if (defined($arg)) {
         if ($arg eq 'DOIT') { 
                $doit=1;
         } elsif ($arg =~ /^([A-Z0-9]{13})$/) {
                $arg=$1;
                print "Adding $arg to the tracking database.\n";
                my $touchfile = "${SPOOL_DIR}/${arg}.txt";
                if (-f $touchfile) {
                      print "  $touchfile already exists.\n";
                } else {
                      system 'touch', $touchfile;
                }
                exit 0;
         } else {
                die "invalid argument $arg. Usage: $0 [DOIT | EA123456789HR ]";
         }
} 

# find all tracked files
opendir DIR, $SPOOL_DIR or die "can't opendir $SPOOL_DIR: $!";
my @files = grep { -f "$SPOOL_DIR/$_" } readdir(DIR);
closedir DIR;

foreach my $t (@files) {
    if ($t =~ /^([A-Z0-9]{13})\.txt$/) {
          $t = $1;
          request_package_status($t);
    }
}

exit 0;
